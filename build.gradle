apply plugin:'groovy'
apply plugin:'idea'
apply plugin:com.smokejumperit.gradle.OneJarPlugin

buildscript {
  repositories {
    mavenRepo url:'http://repo.smokejumperit.com'
  }
  dependencies {
    classpath 'com.smokejumperit:gradle-plugins:0.8.2'
  }
}

repositories {
    maven {
      url "http://mac-mini-1.local/nexus/content/groups/public"
    }
}

dependencies {
    compile "com.yammer.dropwizard:dropwizard-core:0.6.1"
    groovy "org.codehaus.groovy:groovy-all:2.1.1"
}

jar {
    from( configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } ){
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes 'Main-Class': 'com.logicalpractice.dropwizard2.HelloWorldService'
    }
}

// create a self-contained jar that is executable
// the file output name is "build/denominator"
task fat(dependsOn: classes, type: Jar) {
  classifier = "fat"
  destinationDir = buildDir
  archiveName = "helloworld"

  def runtimeDeps = configurations.runtime.collect {
    it.isDirectory() ? it : zipTree(it)
  }

  from (sourceSets*.output.classesDir)

  from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
      exclude "META-INF/*.SF"
      exclude "META-INF/*.DSA"
      exclude "META-INF/*.RSA"
  }

  // really executable jar
  // http://skife.org/java/unix/2011/06/20/really_executable_jars.html

  manifest {
    attributes 'Main-Class': 'com.logicalpractice.dropwizard2.HelloWorldService'
  }

  doLast {
    def srcFile = new File("${destinationDir}/${archiveName}")
    def tmpFile = new File(srcFile.getAbsolutePath() + ".tmp")
    tmpFile.delete()
    tmpFile << "#!/usr/bin/env sh\n"
    tmpFile << 'exec java -jar $0 "$@"' + "\n"
    tmpFile << srcFile.bytes
    tmpFile.setExecutable(true, true)
    tmpFile.renameTo(srcFile)
  }
}